buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    alias(libs.plugins.moduleplugin)
    //alias(libs.plugins.snyk)
}

group GROUP
version VERSION

configurations {
    mockitoAgent
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencyLocking {
    lockAllConfigurations()
}

pmd {
    consoleOutput = true
    toolVersion = "7.7.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files('quality/pmd/java/custom.xml')
    ruleSets = []
}

pmdTest.enabled = false

dependencies {
    implementation libs.bundles.logger
    implementation libs.trivial.chunk

    testImplementation testLibs.bundles.unit.test
    mockitoAgent(testLibs.mockito.core) {
        transitive = false
    }
}

test {
    jvmArgs("-javaagent:${configurations.mockitoAgent.asPath}")
    dependsOn(pmdMain)
    finalizedBy(jacocoTestReport)
    //finalizedBy('snyk-test')
    useJUnitPlatform()
}

task testsJar(type: Jar, dependsOn: [testClasses]) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
    dependsOn(test
            //,'snyk-monitor'
    )
    finalizedBy(javadocJar, sourcesJar, testsJar)
}

jacocoTestReport {
    dependsOn test
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}